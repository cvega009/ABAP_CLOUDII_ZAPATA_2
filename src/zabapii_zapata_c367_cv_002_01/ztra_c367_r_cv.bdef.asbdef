managed implementation in class zbp_tra_c367_r_cv unique;
strict ( 2 );
with draft;

define behavior for ZTRA_C367_R_CV alias Travel
persistent table ztra_c367_a_cv
draft table ztra_c367_d_cv
lock master
total etag LastChangedAt
etag master LocalLastChangedAt
authorization master ( global, instance )

{
  create;
  update;
  delete;
  //------------------------------------------------------------------------------------
  field ( numbering : managed, readonly ) TravelUUID;

  //Campos de lectura
  field ( readonly ) TravelID,
  OverallStatus,
  TotalPrice,
  LastChangedAt,
  LocalCreatedAt,
  LocalCreatedBy,
  LocalLastChangedBy;

  //Campos obligatorios
  field ( mandatory ) CustomerID, AgencyID, BeginDate, EndDate, CurrencyCode;

  field ( features : instance ) BookingFee;
  //------------------------------------------------------------------------------------
  //ACCIONES
  //Acciones de botones
  action ( features : instance, authorization : update ) acceptTravel result [1] $self;
  action ( features : instance, authorization : update ) rejectTravel result [1] $self;

  //Acción con la entidad abstracta
  action ( features : instance, authorization : update ) deductDiscount
    parameter ZAE_TRAVEL_DIS_C367_CV result [1] $self;

  //Acción interna
  internal action reCalcTotalPrice;
  //------------------------------------------------------------------------------------
  //Validaciones
  validation validateCustomer on save { create; field CustomerID; }
  validation validateAgency on save { create; field AgencyID; }
  validation validateDateRange on save { create; field BeginDate, EndDate; }
  //--------------------------------------------------------------------------------------
  //  Determinaciones
  determination setTravelNumber on save { create; }
  determination setStatusToOpen on modify { create; }
  determination calculateTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  determine action validateCustomerID { validation validateCustomer; }
  determine action validateAgencyID { validation validateAgency; }
  determine action validateDates { validation validateDateRange; }
  //--------------------------------------------------------------------------------------
  //Efectos secundarios
  side effects
  {
    field BookingFee affects field TotalPrice;

    determine action validateCustomerID executed on field CustomerId affects messages;
    determine action validateAgencyID executed on field AgencyId affects messages;
    determine action validateDates executed on field BeginDate, field EndDate affects messages;
  }
  //--------------------------------------------------------------------------------------
  //   Draft
  draft action Resume;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;

  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateAgency;
    validation validateDateRange;
  }
  //--------------------------------------------------------------------------------------
  mapping for ztra_c367_a_cv
    {

      //Campos Entidad Root = Campos Tabla de Persistencia
      TravelUUID         = travel_uuid;
      TravelID           = travel_id;
      AgencyID           = agency_id;
      CustomerID         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }

}